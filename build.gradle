plugins {
    id 'org.springframework.boot' version '2.5.13'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'war'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-tomcat'
    implementation('org.springframework:spring-context') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'org.slf4j:jcl-over-slf4j:1.7.30'

    implementation 'com.google.guava:guava:33.3.1-jre'
    implementation 'org.apache.poi:poi-ooxml:5.3.0'
    implementation 'org.dom4j:dom4j:2.1.4'

    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    implementation group: 'commons-net', name: 'commons-net', version: '3.9.0'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation group: 'oro', name: 'oro', version: '2.0.8'
    implementation 'commons-io:commons-io:2.18.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'junit:junit:4.13.1'
}

test {
    useJUnitPlatform()
}

group = 'deposit-dashboard'
version = '2.0.1'
description = 'Dashboard Tool'
sourceCompatibility = '1.11'
targetCompatibility = '1.11'

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from(sourceSets.main.allJava)
}

tasks.register('testsJar', Jar) {
    archiveClassifier.set('tests')
    from(sourceSets.test.output)
}

tasks.register('cleanUI', Delete) {
    group = "BuildUI"
    description = "Remove the dist folder and webapp folder"
    delete './ui/dist', './src/main/resources/static/'
    followSymlinks = true
    doLast {
        println 'UI cleaning done!'
    }
}

tasks.register('installUI', Exec) {
    group = "BuildUI"
    description = "Install the dependencies for UI"
    workingDir './ui'
    commandLine 'npm', 'install'
    doLast {
        println 'UI installation done!'
    }
}

tasks.register('buildUI', Exec) {
    group = "BuildUI"
    description = "Run npm install and build-only"
    dependsOn tasks.installUI
    workingDir './ui'
    commandLine 'npm', 'run', 'build-only'
    doLast {
        println 'UI building done!'
    }
}


tasks.register('copyUI', Copy) {
    group = "BuildUI"
    description = "Copy the files from dist folder to webapp folder"
    dependsOn tasks.buildUI
    from './ui/dist'
    into './src/main/resources/static'
    doLast {
        println 'UI copying done!'
        println 'All UI tasks done!'
    }
}

bootWar {
    dependsOn tasks.copyUI
}

war {
    dependsOn tasks.copyUI
}

bootRun {
    dependsOn tasks.copyUI
}

clean {
    dependsOn tasks.cleanUI
}

build {
    dependsOn tasks.copyUI
}

