import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:3.5.9"
        classpath "io.spring.gradle:dependency-management-plugin:1.1.7.RELEASE"
    }
}

apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "java"
apply plugin: "war"

group = 'deposit-dashboard'
version = '3.0.0'
description = 'Dashboard Tool'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-tomcat'
    implementation 'org.springframework:spring-context'

    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper'

    implementation 'com.google.guava:guava:33.3.1-jre'
    implementation 'org.apache.poi:poi-ooxml:5.4.0'
    implementation 'org.dom4j:dom4j:2.1.4'

    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    implementation group: 'commons-net', name: 'commons-net', version: '3.9.0'
    implementation 'org.apache.commons:commons-lang3:3.18.0'
    implementation group: 'oro', name: 'oro', version: '2.0.8'
    implementation 'commons-io:commons-io:2.18.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'junit:junit:4.13.1'
}

bootWar {
    enabled = true   // optional, bootWar is enabled by default
//    archiveFileName = "deposit-dashboard.war"
}

war {
    enabled = false  // disable the plain WAR
}


test {
    useJUnitPlatform()
    jvmArgs "-Xshare:off"
}

processResources {
    filesMatching('**/application.properties') {
        filter ReplaceTokens, tokens: [
                "app.version": "${version}".toString()
        ]
    }
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from(sourceSets.main.allJava)
}

tasks.register('testsJar', Jar) {
    archiveClassifier.set('tests')
    from(sourceSets.test.output)
}

tasks.register("printVersion") {
    doLast {
        println project.version
    }
}



